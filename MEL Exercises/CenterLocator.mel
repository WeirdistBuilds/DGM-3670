// If you set GiveTotalCenter to anything but 0, it will give a single locator at the center of each object or selected component (verts, edges, etc.).
// If you set MatchRotate to anything but 0, it will match the created locator's rotation to it's located object. If GiveTotalCenter is also on, this does nothing.
// If this tool is used on verts, edges, or faces, it will not create the locator at the correct place unless their shape has had its transforms frozen.

global proc CenterLocator(int $GiveTotalCenter, int $MatchRotate)
{
	string $Selection[] = `ls -sl`;
	string $Locators[];
	float $CurrentPos[];
	float $CurrentRot[];
	string $CurrentName;
	string $NewName;
	string $CurrentNamePieces[];
	int $ArgLength;
	float $Total[];
	float $BoundingBox[];

	for ($i = 0; $i < size($Selection); $i++)
	{
		$CurrentName = $Selection[$i];
		tokenize $CurrentName ".[]_: " $CurrentNamePieces;
		$NewName = "";
		for ($j = 0; $j < size($CurrentNamePieces); $j++)
		{
			$NewName += ($CurrentNamePieces[$j] + "_");
		}
		$CurrentName = ($NewName + "_Loc");	
	
		$BoundingBox = `xform -q -a -boundingBox $Selection[$i]`;
		$CurrentPos[0] = ($BoundingBox[0] + $BoundingBox[3]) / 2;
		$CurrentPos[1] = ($BoundingBox[1] + $BoundingBox[4]) / 2;
		$CurrentPos[2] = ($BoundingBox[2] + $BoundingBox[5]) / 2;
		spaceLocator -n $CurrentName;
		xform -t $CurrentPos[0] $CurrentPos[1] $CurrentPos[2] $CurrentName;
		
		if ($MatchRotate != 0 && $GiveTotalCenter == 0)
		{
			$CurrentRot = `xform -q -ro $Selection[$i]`;
			xform -ro $CurrentRot[0] $CurrentRot[1] $CurrentRot[2] $CurrentName;
		}
		
		$Locators[$i] = $CurrentName;		
	}

	if ($GiveTotalCenter != 0)
	{
		for ($i = 0; $i < size($Locators); $i++)
		{
			$CurrentPos = `xform -q -t $Locators[$i]`;
			$Total[0] += $CurrentPos[0];
			$Total[1] += $CurrentPos[1];
			$Total[2] += $CurrentPos[2];
		}
		
		$Total[0] /= size($Locators);
		$Total[1] /= size($Locators);
		$Total[2] /= size($Locators);
		spaceLocator -n "TrueCenter_Loc";
		xform -t $Total[0] $Total[1] $Total[2] "TrueCenter_Loc";
		delete $Locators;
	}
}

CenterLocator(1, 0);